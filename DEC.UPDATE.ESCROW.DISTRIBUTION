[=======================================================================================================================
Author: Devon Connors
File Name: UPDATE.ESCROW.DISTRIBUTION
Description: A dialog box appears allowing the user to select the loan they would like to manipulate.  
             The user can change the Next Distribution Date or the Next Distribution Amount using
             this tool.
             
Date Created: 6/5/18
Revision History:
DATE:                       AUTHOR:                              DESCRIPTION:
-----                       ----------------------------         ----------------------------------------
12/29/2020					Tom Strohecker							Added logic to include additional fields to edit
========================================================================================================================]

WINDOWS

TARGET=ACCOUNT

DEFINE
  #INCLUDE "RD.GETDATA.DEF"
  [FLAGS]
  CHANGEDISTDATE=NUMBER
  CHANGEDISTAMT=NUMBER
  ESCROWINSTANCE=NUMBER

  [DIALOGBOX]
  NEXTDISTDATE=DATE
  NEXTDISTAMT=MONEY
  LOANID=NUMBER
  ESCROWTYPE=NUMBER
  
  [LOAN]
  ESCROWAMT=MONEY
  LID=CHARACTER
  
  [ESCROW RECORD]
  OGDISTDATE=DATE
  ESCROWLOC=NUMBER
  
  [ESCROW ANALYSIS]
  OGANALYSISDATE=DATE
  ANALYSISLOC=NUMBER
  
  [FMPERFORM]
  ERRORTEXT=CHARACTER
  
  [DEFAULT VARIABLES]
  NEWPOLAMT=MONEY
  DFTPOLAMT=MONEY
  NEWPOLNUM=CHARACTER
  DFTPOLNUM=CHARACTER
  NEWVENDORNAME=CHARACTER
  DFTVENDORNAME=CHARACTER
  NEWVENNUM=CHARACTER
  DFTVENNUM=CHARACTER
  NEWORIGDATE=DATE
  DFTORIGDATE=DATE
  NEWPOLEXDATE=DATE
  DFTPOLEXDATE=DATE
  NEWPAYPORT=MONEY
  DFTPAYPORT=MONEY
  DFTNEXTDISTDATE=DATE
  DFTNEXTDISTAMT=MONEY
  TRUE=1
  FALSE=0
  X=NUMBER
  
  MN=NUMBER
  DY=NUMBER
  YR=NUMBER
  TEMPDATE=DATE
  VALUEARRAY = CHARACTER ARRAY(5,9)
  CONFIRM=CHARACTER
  TEMPTEXT= CHARACTER
  DOFM=NUMBER
  CHANGECOUNT=NUMBER
END[END IF]

SETUP
 CALL SELECTLOAN
 CALL PICKESCROW
 CALL GETDEFAULTS
 CALL PROMPTDATA
 CALL VALIDATEUPDATES
 CALL CONFIRMATIONSCREEN
    
 CALL CHECKDATA
[CALL PICKESCROW]
END[END SETUP]

PRINT TITLE="Update Escrow Distribution Information" 
 IF NEXTDISTAMT<ESCROWAMT THEN
 DO
  POPUPMESSAGE(2,"Next Distribution Amount less than the Escrow Amount -- Terminating")
  TERMINATE
 END 
 IF DOFM=TRUE THEN 
 DO
  CALL DETERMINEFM
 END[IF] 
 
END[END PRINT TITLE]

PROCEDURE CHECKDATA
  CHANGEDISTDATE=FALSE
  CHANGEDISTAMT=FALSE
  
  IF NEXTDISTDATE='--/--/----' AND NEXTDISTAMT=$0.00 THEN
    DO
      POPUPMESSAGE(2,"Invalid Input")
      TERMINATE
    END[END IF]
  ELSE IF NEXTDISTDATE='--/--/----' AND NEXTDISTAMT<>$0.00 THEN
    DO
      CHANGEDISTAMT=TRUE
    END[END ELSE IF]
  ELSE IF NEXTDISTDATE<>'--/--/----' AND NEXTDISTAMT=$0.00 THEN
    DO
      CHANGEDISTDATE=TRUE
    END[END ELSE IF]
  ELSE
    DO
      CHANGEDISTDATE=TRUE
      CHANGEDISTAMT=TRUE
    END[END ELSE]
    
  IF CHANGEDISTDATE=TRUE THEN
    DO
      IF NEXTDISTDATE < SYSTEMDATE THEN
        DO
          POPUPMESSAGE(2,"Date is prior to System's Date -- Terminating")
          TERMINATE
        END[END IF]
    END[END IF]
END[END CHECKDATA]

PROCEDURE PICKESCROW
  DIALOGSTART("Update Escrow Record",200%,0)
  
  DIALOGPROMPTCOMBOSTART("Select the Appropriate Escrow:",0)
  FOR EACH LOAN WITH LOAN:ID = LID
    DO
      FOR EACH LOAN ESCROW WITH LOAN ESCROW:EXPIRATIONDATE>SYSTEMDATE
      DO
        DIALOGPROMPTCOMBOOPTION(LOAN ESCROW:LOCATOR, GETDATACHAR(GETPARAMESCROWTYPEDESCRIPTION,LOAN ESCROW:ESCROWTYPE))
      END[END FOR EACH LOAN]
    END[END FOR EACH LOAN]
  DIALOGPROMPTCOMBOEND
  
  DIALOGDISPLAY
    ESCROWLOC=ENTERNUMBER("Select the Appropriate Escrow:",0)
  DIALOGCLOSE
 
END[END PICKESCROW]

PROCEDURE DETERMINEFM
  IF CHANGEDISTDATE=TRUE AND CHANGEDISTAMT=FALSE THEN
    DO   
          CALL UPDATEDISTDATE 
    END[END IF]
  ELSE IF CHANGEDISTDATE=FALSE AND CHANGEDISTAMT=TRUE THEN
  DO
   CALL UPDATEDISTAMT
  END[END ELSE IF]
  ELSE IF CHANGEDISTDATE=TRUE AND CHANGEDISTAMT=TRUE THEN
  DO
   CALL UPDATEDISTDATE
   CALL UPDATEDISTAMT
  END[END IF]
  IF NEWPOLAMT <>$0.00 AND DFTPOLAMT<>NEWPOLAMT THEN 
  DO
   CALL UPDATEPOLAMT
  END[IF]
  IF NEWPOLNUM <>"" AND DFTPOLNUM<>NEWPOLNUM THEN 
  DO
   CALL UPDATEPOLNUM
  END[IF]   
  IF NEWVENNUM<>"" AND DFTVENNUM<>NEWVENNUM THEN 
  DO
   CALL UPDATEVENNUM
  END[IF]
  IF NEWORIGDATE<>'--/--/----' AND DFTORIGDATE<>NEWORIGDATE THEN 
  DO
   CALL UPDATEORIGDATE
  END[IF]
  IF NEWPOLEXDATE<>'--/--/----' AND DFTPOLEXDATE<>NEWPOLEXDATE THEN 
  DO
   CALL UPDATEPOLEXDATE
  END[IF]
  IF NEWPAYPORT<>$0.00 AND DFTPAYPORT<>NEWPAYPORT THEN 
  DO
   CALL UPDATEPAYPORT
  END[IF]
END[END DETERMINEFM]

PROCEDURE UPDATEDISTDATE
  FMPERFORM REVISE LOAN LID ESCROW LOC ESCROWLOC(1,0,ERRORTEXT)
    DO
      SET NEXTDISTRIBDATE TO NEXTDISTDATE
      SET DAY1 TO DAY(NEXTDISTDATE)
    END[END FMPERFORM]
  
  CALL CHECKERROR 
  
  IF OGANALYSISDATE > OGDISTDATE-45 THEN
    DO
      CALL UPDATENEXTANALYSISDATE
    END[END IF]
END[END UPDATEDISTDATE]

PROCEDURE UPDATEDISTAMT
  FMPERFORM REVISE LOAN LID ESCROW LOC ESCROWLOC(1,0,ERRORTEXT)
    DO
      SET NEXTDISTRIBAMOUNT TO NEXTDISTAMT
    END[END FMPERFORM]
  
  CALL CHECKERROR
END[END UPDATEDISTAMT]

PROCEDURE UPDATEBOTH
  FMPERFORM REVISE LOAN LID ESCROW LOC ESCROWLOC(1,0,ERRORTEXT)
    DO
      SET NEXTDISTRIBDATE TO NEXTDISTDATE
      SET DAY1 TO DAY(NEXTDISTDATE)
      SET NEXTDISTRIBAMOUNT TO NEXTDISTAMT
    END[END FMPERFORM]
  
  CALL CHECKERROR
  CALL UPDATENEXTANALYSISDATE
END[END UPDATEBOTH]

PROCEDURE UPDATENEXTANALYSISDATE
  [FMPERFORM REVISE LOAN LID ESCROWANALYSIS LOC ANALYSISLOC(1,0,ERRORTEXT)
    DO
      SET NEXTANALYSISDATE TO NEXTDISTDATE-45
    END[END FMPERFORM]
    ]
  CALL CHECKERROR
END[END UPDATENEXTANALYSISDATE]

PROCEDURE CHECKERROR
  IF ERRORTEXT<>"" THEN
    DO
      POPUPMESSAGE(0,"Change could not be completed - " + ERRORTEXT)
      TERMINATE
    END
END[END CHECKERROR]

PROCEDURE SELECTLOAN
 DIALOGSTART("Update Escrow Record",200%,0) 
 DIALOGPROMPTCOMBOSTART("Select the Appropriate Loan:",0)
 FOR EACH LOAN WITH LOAN:CLOSEDATE='--/--/----' AND
                    LOAN:CHARGEOFFDATE='--/--/----' AND
                   (LOAN:TYPE>=50 OR LOAN:TYPE<=60)
 DO
  ESCROWINSTANCE=FALSE
  FOR EACH LOAN ESCROW
  DO
   ESCROWINSTANCE=TRUE
  END[END FOR EACH LOAN ESCROW]        
  IF ESCROWINSTANCE=TRUE THEN
  DO
   DIALOGPROMPTCOMBOOPTION(VALUE(LOAN:ID),LOAN:ID+"-"+LOAN:DESCRIPTION)
  END[END IF]
 END[END FOR EACH LOAN]
 DIALOGPROMPTCOMBOEND    
 DIALOGDISPLAY
 LOANID=ENTERNUMBER("Select the Appropriate Loan:",0)
 DIALOGCLOSE
  
 LID=FORMAT("9999",LOANID)
END[PROCEDURE SELECTLOAN]

PROCEDURE GETDEFAULTS
 VALUEARRAY(0,0)="Policy Amount"
 VALUEARRAY(3,0)="MONEY"
 VALUEARRAY(0,1)="Policy Number"
 VALUEARRAY(3,1)="CHARACTER"
 VALUEARRAY(0,2)="Vendor Name"
 VALUEARRAY(3,2)="CHARACTER"
 VALUEARRAY(0,3)="Vendor Number"
 VALUEARRAY(3,3)="CHARACTER"
 VALUEARRAY(0,4)="Origination Date"
 VALUEARRAY(3,4)="DATE"
 VALUEARRAY(0,5)="Policy Expiration Date"
 VALUEARRAY(3,5)="DATE"
 VALUEARRAY(0,6)="Payment Portion"
 VALUEARRAY(3,6)="MONEY"
 VALUEARRAY(0,7)="Next Distribution Date"
 VALUEARRAY(3,7)="DATE"
 VALUEARRAY(0,8)="Next Distribution Amount"
 VALUEARRAY(3,8)="MONEY"
 FOR EACH LOAN WITH LOAN:ID=LID
 DO
  ESCROWAMT=LOAN:ESCROWAMOUNT
  FOR EACH LOAN ESCROW WITH LOAN ESCROW:LOCATOR=ESCROWLOC
  DO
   DFTPOLAMT=LOAN ESCROW:POLICYAMOUNT
   VALUEARRAY(1,0)=FORMAT("####9.99",DFTPOLAMT)
   DFTPOLNUM=LOAN ESCROW:POLICYNUMBER
   VALUEARRAY(1,1)=DFTPOLNUM
   DFTVENDORNAME=LOAN ESCROW:VENDORNAME
   VALUEARRAY(1,2)=DFTVENDORNAME
   DFTVENNUM=LOAN ESCROW:VENDORNUMBER
   VALUEARRAY(1,3)=DFTVENNUM
   DFTORIGDATE=LOAN ESCROW:POLICYORIGDATE
   VALUEARRAY(1,4)=FORMAT("99/99/9999",DFTORIGDATE)
   DFTPOLEXDATE=LOAN ESCROW:POLICYEXPIREDATE
   VALUEARRAY(1,5)=FORMAT("99/99/9999",DFTPOLEXDATE)
   DFTPAYPORT=LOAN ESCROW:PAYMENTPORTION
   VALUEARRAY(1,6)=FORMAT("####9.99",DFTPAYPORT)
   DFTNEXTDISTDATE=LOAN ESCROW:NEXTDISTRIBDATE
   VALUEARRAY(1,7)=FORMAT("99/99/9999",DFTNEXTDISTDATE)
   DFTNEXTDISTAMT=LOAN ESCROW:NEXTDISTRIBAMOUNT
   VALUEARRAY(1,8)=FORMAT("####9.99",DFTNEXTDISTAMT)
   
  END[FOR EACH LOAN ESCROW]
  FOR EACH LOAN ESCROWANALYSIS
  DO
   OGANALYSISDATE=LOAN ESCROWANALYSIS:NEXTANALYSISDATE
   ANALYSISLOC=LOAN ESCROWANALYSIS:LOCATOR
  END
 END[FOR LOAN]
END[PROCEDURE GETDEFAULTS]

PROCEDURE PROMPTDATA

DIALOGSTART("Update Escrow Record",200%,0)
DIALOGSTARTGROUPBOX("Distribution Information")  
 DIALOGPROMPTDATE("Next Distribution Date: ",DFTNEXTDISTDATE)
 DIALOGPROMPTMONEY("Next Distribution Amount: ",DFTNEXTDISTAMT)
DIALOGENDGROUPBOX  
  [NEW PROMPTS]
  DIALOGSTARTGROUPBOX("Policy Information")
   DIALOGPROMPTMONEY("Policy Amount: ",DFTPOLAMT)
   DIALOGPROMPTCHAR("Policy Number:",40,DFTPOLNUM)
   DIALOGPROMPTDATE("Policy Origination Date:",DFTORIGDATE)
   DIALOGPROMPTDATE("Policy Expiration Date:",DFTPOLEXDATE)
   DIALOGPROMPTMONEY("Payment Portion: ",DFTPAYPORT)     
  DIALOGENDGROUPBOX  
  
  DIALOGSTARTGROUPBOX("Vendor Information")  
   [DIALOGPROMPTCHAR("Vendor Name:",40,DFTVENDORNAME)]
   DIALOGPROMPTCHAR("Vendor Number:",40,DFTVENNUM) 
  DIALOGENDGROUPBOX  
  
  DIALOGDISPLAY
  
  NEXTDISTDATE=ENTERDATE("Next Distribution Date: ",DFTNEXTDISTDATE)
  NEXTDISTAMT=ENTERMONEY("Next Distribution Amount: ",DFTNEXTDISTAMT) 
  NEWPOLAMT=ENTERMONEY("Policy Amount: ",DFTPOLAMT) 
  NEWPOLNUM=ENTERCHARACTER("Policy Number:",40,DFTPOLNUM)
  NEWORIGDATE=ENTERDATE("Policy Origination Date:",DFTORIGDATE)
  NEWPOLEXDATE=ENTERDATE("Policy Expiration Date:",DFTPOLEXDATE)
  NEWPAYPORT=ENTERMONEY("Payment Portion: ",DFTPAYPORT)
  [NEWVENDORNAME=ENTERCHARACTER("Vendor Name:",40,DFTVENDORNAME)]
  NEWVENNUM=ENTERCHARACTER("Vendor Number:",40,DFTVENNUM)
  
  VALUEARRAY(2,0)=FORMAT("####9.99",NEWPOLAMT)
  VALUEARRAY(2,1)=NEWPOLNUM
  VALUEARRAY(2,2)=NEWVENDORNAME
  VALUEARRAY(2,3)=NEWVENNUM
  VALUEARRAY(2,4)=FORMAT("99/99/9999",NEWORIGDATE)
  VALUEARRAY(2,5)=FORMAT("99/99/9999",NEWPOLEXDATE)
  VALUEARRAY(2,6)=FORMAT("####9.99",NEWPAYPORT)
  VALUEARRAY(2,7)=FORMAT("99/99/9999",NEXTDISTDATE)
  VALUEARRAY(2,8)=FORMAT("####9.99",NEXTDISTAMT)
  
  DIALOGCLOSE
END[PROCEDURE PROMPTDATA]

PROCEDURE UPDATEVENNUM
 FMPERFORM REVISE LOAN LID ESCROW LOC ESCROWLOC(1,0,ERRORTEXT)
 DO
  SET VENDORNUMBER TO NEWVENNUM
 END[END FMPERFORM] 
 CALL CHECKERROR 
END[PROCEDURE UPDATEVENAME]

PROCEDURE UPDATEPAYPORT
 FMPERFORM REVISE LOAN LID ESCROW LOC ESCROWLOC(1,0,ERRORTEXT)
 DO
  SET PAYMENTPORTION TO NEWPAYPORT
 END[END FMPERFORM] 
 CALL CHECKERROR 
END[PROCEDURE UPDATEPAYPORT]

PROCEDURE UPDATEPOLEXDATE
 FMPERFORM REVISE LOAN LID ESCROW LOC ESCROWLOC(1,0,ERRORTEXT)
 DO
  SET POLICYEXPIREDATE TO NEWPOLEXDATE
 END[END FMPERFORM] 
 CALL CHECKERROR 
END[PROCEDURE UPDATEPOLEXDATE]


PROCEDURE UPDATEORIGDATE
 FMPERFORM REVISE LOAN LID ESCROW LOC ESCROWLOC(1,0,ERRORTEXT)
 DO
  SET POLICYORIGDATE TO NEWORIGDATE
 END[END FMPERFORM] 
 CALL CHECKERROR 
END[PROCEDURE UPDATEORIGDATE]

PROCEDURE UPDATEPOLNUM
 FMPERFORM REVISE LOAN LID ESCROW LOC ESCROWLOC(1,0,ERRORTEXT)
 DO
  SET POLICYNUMBER TO NEWPOLNUM
 END[END FMPERFORM] 
 CALL CHECKERROR 
END[PROCEDURE UPDATEPOLNUM]

PROCEDURE UPDATEPOLAMT
 FMPERFORM REVISE LOAN LID ESCROW LOC ESCROWLOC(1,0,ERRORTEXT)
 DO
  SET POLICYAMOUNT TO NEWPOLAMT
 END[END FMPERFORM] 
 CALL CHECKERROR 
END[PROCEDURE UPDATEPOLAMT]

PROCEDURE VALIDATEUPDATES
 CONFIRM="0"
 CHANGECOUNT=0
 FOR X = 0 TO 8
 DO
  IF VALUEARRAY(3,X)="CHARACTER" THEN 
  DO
   IF X = 1 AND VALUEARRAY(2,X) ="" THEN 
   DO
    CONFIRM="-1"
    VALUEARRAY(4,X)=""
   END[IF]
   ELSE IF VALUEARRAY(2,X)<>VALUEARRAY(1,X) AND X<>2 THEN 
   DO
    CHANGECOUNT=CHANGECOUNT+1
    VALUEARRAY(4,X)="1"
   END[ELSE IF]
   ELSE 
    VALUEARRAY(4,X)="0"   
  END [IF]
  ELSE IF VALUEARRAY(3,X)="MONEY" THEN
  DO
   IF VALUEARRAY(2,X)<>VALUEARRAY(1,X) THEN 
   DO 
    VALUEARRAY(4,X)="1"
    CHANGECOUNT=CHANGECOUNT+1
   END[DO]
   ELSE 
    VALUEARRAY(4,X)="0" 
  END[ELSE IF]
  ELSE IF VALUEARRAY(3,X)="DATE" THEN
  DO
   MN=VALUE(SEGMENT(VALUEARRAY(2,X),1,2))
   DY=VALUE(SEGMENT(VALUEARRAY(2,X),4,5))
   YR=VALUE(SEGMENT(VALUEARRAY(2,X),7,10))
   TEMPDATE=DATE(MN,DY,YR)
   IF SYSTEMDATE>TEMPDATE THEN 
   DO
    IF VALUEARRAY(2,X)<>VALUEARRAY(1,X) THEN 
     VALUEARRAY(4,X)=""
    ELSE 
    VALUEARRAY(4,X)="0"
   END[IF]
   ELSE
   DO
    IF VALUEARRAY(2,X)<>VALUEARRAY(1,X) THEN 
    DO
     CHANGECOUNT=CHANGECOUNT+1
     VALUEARRAY(4,X)="1"
    END[IF]
    ELSE 
     VALUEARRAY(4,X)="0"
   END[ELSE]
  END[ELSE IF]
 END[FOR X]
END[PROCEDURE VALIDATEUPDATES]

PROCEDURE CONFIRMATIONSCREEN
 TEMPTEXT=""
 [CONFIRM="0"]
 DOFM=FALSE
  
  DIALOGSTART("Escrow Update Confirmation",50%,0)
  DIALOGINTROTEXT("")
  
  IF CHANGECOUNT>0 THEN
  DO
   FOR X = 0 TO 8
   DO
    IF VALUEARRAY(4,X)="1" AND CONFIRM<>"-1" THEN 
    DO
     TEMPTEXT = VALUEARRAY(0,X)+" changed from "+VALUEARRAY(1,X)+" to "+VALUEARRAY(2,X)
     DIALOGINTROTEXT(TEMPTEXT)
    END [IF]
    ELSE IF VALUEARRAY(4,X)="" THEN 
    DO
     IF VALUEARRAY(3,X)="CHARACTER" THEN 
     DO
      TEMPTEXT = VALUEARRAY(0,X)+" cannot be blank! Changes cannot be made!"
      CONFIRM = "-1"
     END[IF]
     ELSE IF CONFIRM<>"-1" THEN
      TEMPTEXT = VALUEARRAY(0,X)+" changed from "+VALUEARRAY(1,X)+" to "+VALUEARRAY(2,X)+"**Date is older than systemdate**"
     DIALOGINTROTEXT(TEMPTEXT)
    END [ELSE] 
   END[FOR X]
   DIALOGSTARTGROUPING
   IF CONFIRM <> "-1" THEN 
   DO
    DIALOGPROMPTYESNO("Confirm Changes",0)
   END[IF]
   DIALOGENDGROUPING
 END[IF]
 ELSE
 DO
  TEMPTEXT = "No changes were made."
  DIALOGINTROTEXT(TEMPTEXT)
 END[ELSE]
  DIALOGDISPLAY
 
  IF CONFIRM<>"-1" THEN 
  DO
   CONFIRM=ENTERYESNO("Confirm Changes","N")
  END[IF]
  IF CONFIRM="1" OR CONFIRM="y" OR CONFIRM="Y" THEN
   DOFM=TRUE
  ELSE
   DOFM=FALSE
 DIALOGCLOSE
END[PROCEDURE CONFIRMATIONSCREEN]
